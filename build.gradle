plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
    id 'de.undercouch.download' version '5.3.0'
}

group = 'users-auth'
version = '0.0.1-SNAPSHOT'
description = 'Authorization with Auth0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/austral-ingsis/*")
        credentials {
            username = findProperty("gpr.user")
            password = findProperty("gpr.key")
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

/* =======================
   New Relic Java Agent
   ======================= */

tasks.register("downloadNewRelic", Download) {
    src "https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip"
    dest file("$buildDir/newrelic/newrelic-java.zip")
}

tasks.register("unzipNewRelic", Copy) {
    dependsOn "downloadNewRelic"
    from zipTree("$buildDir/newrelic/newrelic-java.zip")
    into "$buildDir/newrelic"
}

/* =======================
   Testing / Coverage
   ======================= */

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
}

jacocoTestReport {
    dependsOn tasks.test
    reports {
        xml.required = true
        html.required = true
    }
}

/* =======================
   Quality tools
   ======================= */

spotless {
    java {
        target 'src/**/*.java'
        eclipse().configFile("config/eclipse-java-style.xml")
        importOrder()
        removeUnusedImports()
        lineEndings = 'UNIX'
    }
}

checkstyle {
    toolVersion = '10.12.0'
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}