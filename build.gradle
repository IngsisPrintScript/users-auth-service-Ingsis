plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
}

group = 'users-auth'
version = '0.0.1-SNAPSHOT'
description = 'Authorization with Auth0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/austral-ingsis/*")
        credentials {
            username = findProperty("gpr.user")
            password = findProperty("gpr.key")
        }
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register("installGitHooks") {
    onlyIf {
        !System.getProperty("os.name").toLowerCase().contains("win")
    }

    doLast {
        exec {
            commandLine "sh", "./scripts/install-hooks.sh"
        }
    }
}
tasks.named("build") {
    dependsOn("installGitHooks")
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.jacocoTestReport
}

//jacoco {
//	toolVersion = "0.8.10"
//}

jacocoTestReport {
	dependsOn tasks.test
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
    java {
        target 'src/**/*.java'
        eclipse().configFile("config/eclipse-java-style.xml")
        importOrder()
        removeUnusedImports()
        lineEndings = 'UNIX'
    }
}
checkstyle {
    toolVersion = '10.12.0'
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}